# Ex 7: Scroll of Caesar

On the fourth day, walking on the road to Seville, an eagle landed on the boy's arm. In a leather pouch tied to its legs was a scroll,  with a message from the Alchemist.

```text
Thirteen times of what you have Left, you owe Caesar
Gur havirefr pbafcverf gb uryc gur Qernzre
```

The boy was puzzled. The first line was confusing. Caesar's Roman Empire was long gone. Surely the Alchemist meant something other than paying tax on the treasure he found! The garbled words on the second line made no sense either. 

Suddenly, intuition struck a chord. The Alchemist had once told him about *Caesar's Cypher*, a secret language that the Masters used to hide information in plain sight. The second line was garbled because it was encrypted with [Caesar's Cypher](https://en.wikipedia.org/wiki/Caesar_cipher). The cypher simply substituted each character in the plain text with another character, a fixed number of positions down the alphabet. "Thirteen" positions to the "Left", to be precise. The first line of the message from the Alchemist was the hint. The second line was the result of this substitution, the encoded message.

## Coding Exercise

Just like in the previous chapter, we will proceed to write code, version by version. This approach has the tangential benefit of practice through repetition, which will help solidify the patterns of Elixir programming in our minds. 

We will also see how to break down a seemingly big problem into smaller and smaller sub-problems, solving these sub-problems and building up an overall solution from there.

### Version 0.1

In our first attempt at solving the problem of decoding a sentence using Caesar's cypher, we will focus on the smallest possible sub-problem first: decoding a *single* character.

Type in the code listed below into your editor and observe the result; for yourselves.

```elixir
defmodule TextAlchemy do
  @moduledoc """
  Provides text manipulation related functions.
  """

  @doc """
  Rotates a single character by 13 positions.
  """
  def rot13c(character) do

    cond do
      character == "a" -> "n"
      character == "b" -> "o"
      character == "c" -> "p"
      character == "d" -> "q"
      character == "e" -> "r"
      character == "f" -> "s"
      character == "g" -> "t"
      character == "h" -> "u"
      character == "i" -> "v"
      character == "j" -> "w"
      character == "k" -> "x"
      character == "l" -> "y"
      character == "m" -> "z"
      character == "n" -> "a"
      character == "o" -> "b"
      character == "p" -> "c"
      character == "q" -> "d"
      character == "r" -> "e"
      character == "s" -> "f"
      character == "t" -> "g"
      character == "u" -> "h"
      character == "v" -> "i"
      character == "w" -> "j"
      character == "x" -> "k"
      character == "y" -> "l"
      character == "z" -> "m"
      true             -> :error
    end
  end

end

IO.puts "Rotating 'a' gives '#{TextAlchemy.rot13c('a')}'"
IO.puts "Rotating 'b' gives '#{TextAlchemy.rot13c('b')}'"
IO.puts "Rotating 'm' gives '#{TextAlchemy.rot13c('m')}'"
IO.puts "Rotating 'n' gives '#{TextAlchemy.rot13c('n')}'"
IO.puts "Rotating 'A' gives '#{TextAlchemy.rot13c('A')}'"

```

#### Notes on Version 0.1

1. `cond` is essentially `if`on steroids (i.e, a very powerful alternative to `if`). Whereas `if` helps us check for a single condition and execute an expression if the condition is true, `cond` is used when we have to check many different conditions and execute the corresponding expression of the condition that evaluates to true.
2. Note that only one resulting expression is returned. `cond` checks the list of conditions in the written order and returns the expression of the first condition that evaluates to true.
3. We have to add `true -> :error` to the very end of the list of `condition -> return value` lines, so as to handle the case when *none* of the previous conditions evaluates to true. Since `true` is always true (:D), `cond` will therefore always match and return `:error` in such cases. If we don't add this line, and no conditions match while running this code, Elixir will throw an error (`CondClauseError`).
4. Even though in this particular code example, the conditions listed are very similar to each other, in the general case, `cond` can check for the *truth value* of any valid Elixir expression. (Side note: In computer science, expressions that evaluates to a boolean value, i.e, `true` or `false` are alternatively known as **predicates**.)

### Version 0.2

In our second iteration on solving the problem of decoding a sentence using Ceasar's cypher, we will improve upon our solution to decode a single *character* and then pick the next sub-problem to solve: decoding a *word*. 

Replace the previous code with the code listed below. Remember to type in all the code in by yourselves. Run the program and observe the result.

```elixir
defmodule TextAlchemy do
  @moduledoc """
  Provides text manipulation related functions.
  """

  @doc """
  Rotates a single character by 13 positions.
  """
  def rot13c(character) do

    case character do
       "a" -> "n"
       "b" -> "o"
       "c" -> "p"
       "d" -> "q"
       "e" -> "r"
       "f" -> "s"
       "g" -> "t"
       "h" -> "u"
       "i" -> "v"
       "j" -> "w"
       "k" -> "x"
       "l" -> "y"
       "m" -> "z"
       "n" -> "a"
       "o" -> "b"
       "p" -> "c"
       "q" -> "d"
       "r" -> "e"
       "s" -> "f"
       "t" -> "g"
       "u" -> "h"
       "v" -> "i"
       "w" -> "j"
       "x" -> "k"
       "y" -> "l"
       "z" -> "m"
       _   -> :error
    end
  end

  @doc """
  Applies the rot13c function to all characters in a word
  """
  def rot13w(word) do

   word
   |> String.downcase
   |> String.graphemes
   |> Enum.map(&rot13c/1)
   |> Enum.into("")

  end

end

IO.inspect TextAlchemy.rot13w("abcd")
IO.inspect TextAlchemy.rot13w("NoPq")
# Decoding the first word on the second line of the Alchemist's message
IO.inspect TextAlchemy.rot13w("Gur")

```

#### Notes on Version 0.2

1. Notice that we have replaced `cond` with `case`. Observe the differences between them. `case` allows us to compare the value of an expression (in thise case the value of `character`) against many patterns. Even though `cond` is more general, since we are comparing the value of `character` with multiple possible values, replacing it with `case` allows us to write a more concise and arguably better readable code.
2. Just like we had a "catch all" phrase at the end of `cond`, for `case` we have `_ -> :error` which will match *any* value of the `case` expression. Note that we are using `_ ->` instead of `true ->`. This is because `case` tries for matching a *pattern of value* than the *boolean value* of an expression after evaluation, as is the case for `cond`.
3. Pattern matching is a very useful and frequently used feature of the Elixir language. If you remember Elixir calls `=`, the *match operator*. Let's try out somethings in IEx to dig a little deeper into pattern matching.
  
  ```iex
  iex(1)> 1 = x
 (CompileError) iex:1: undefined function x/0
iex(1)> x = 1
1
iex(2)> x
1
iex(3)> 1 = x
1
iex(4)> 2 = x
** (MatchError) no match of right hand side value: 1
  ```
  * At a fresh IEX console, first we attempted to pattern match `1 = x`, but Elixir returned an error. This is because we haven't told elixir what value to *bind* to the variable `x`. That is to say the name `x` has not been yet given to any *value*, or that the variable `x` is not *assigned* yet.
  * Next we *bind* the value `1` to `x` by typing in `x = 1` and Elixir says, "OK, now the variable `x` has been *assigned* the value `1`.
  * Let's double check. Typing in `x` into IEx returns the value `1`. Alright!
  * Now for the mind bending twist. Typing in `1 = x`, returns the value `1`! Elixir is essentially saying, "*Yes I was able to match the right hand side value (`x` which is `1`) to the left hand side value, `1`*.
  * So what happens if we try `2 = x`? Elixir promptly returns an error (`MatchError`). 
  * Pattern matching works not only for integers as we have seen here, but also for strings or even lists and more! We will see more creative uses of pattern matching as we go along.

4. Coming to the function `rot13w` which takes 1 argument `word`. Its time to introduce to you another star feature of Elixir, `|>`, the pipe operator. `|>` *pipes* data (allows data to flow) from one expression/function into another function. Let's explore `|>` in a fresh IEx console
  
  ```iex
  iex(1)> "CAESAR" |> String.downcase
  "caesar"
  iex(2)> "CAESAR" |> String.downcase |> String.graphemes
  ["c", "a", "e", "s", "a", "r"]
  ```
  * `downcase` is a function defined in the `String` module that transforms an uppercased string into lowercase. It takes one string argument. `|>` pipes the string `"CAESAR"` into the first (and the only) argument of String.downcase and the result is downcased `"caesar"`.
  * Next we extend this *pipeline* by taking the output of `String.downcase` and passing it into the first (and as it happens, the only) argument of `String.graphemes` function. `graphemes` is another function in the `String` module which takes in a string input and returns the individual letters in the word as a list.

5. Coming to the next stage in our "pipeline", we come across a very useful function named `Enum.map`. `map` which belongs to the `Enum` module is an interesting function in that, the argument passed to `map` is another function.  So far we have seen only functions that takes datatypes like string or integer etc as argument. The ability to have functions that can take other functions as input makes Elixir somewhat special. Programming languages that inherently support this style of programming are generally called *functional programming languages*. And functions that takes other functions as inputs are known as *higher order functions*. You could say Elixir treats both data and function as first class citizens and this is one of the reaons why Elixir is a truly productive and fun programming language useful for build things in.

6. Coming back to `Enum.map`, suppose we have a list of things. And suppose, we want to transform each item in the list in a certain way and want the result of this transformation of each item as an output list. What we could do in Elixir is, define how the transformation will work on an individual item as a function. Next we pass in the input list and this function to the `Enum.map` function. `Enum.map` will "map" the input list to the output list by applying the function passed in on each individual item in the input list. Let's make this clear using an example in IEx

  ```iex
  iex(1)> Enum.map(["c", "a", "e", "s", "a", "r"], &String.upcase/1)
  ["C", "A", "E", "S", "A", "R"]
  ```   
  * The first argument to `Enum.map` is a list of letters of the word "caesar" all in lower case. The second argument to `Enum.map` is a function `String.upcase` which as you might guess, does the opposite of `String.downcase` function, i.e capitalize the input passed to it. `Enum.map` applies the `String.upcase` function to each letter in the list and returns the result as an output list.
  * Notice how we have to use `&` before the function name while passing in a function to another function in Elixir. Also notice the `/1`. In Elixir the "full name" of a function is to have both the module name (`String`) and the number of arguments that the function takes which is in this case 1, which is what the `/1` denotes. So Enum.map is "fully specified" in Elixir `Enum.map/2` because it takes two arguments.

7. In our case, what we passed into `Enum.map` is the function we previously defined `TextAlchemy.rot13c/1` (We can drop the module name if we are referring to the function in the same module). The *first* argument to `Enum.map` is supplied by the `|>` operator. The second argument, which is the function that is used for transforming individual elements of the list is specified as `&rot13c/1`. The result is a list of characters, each rotated 13 positions to the left. You can verify it by trying it out in an IEx console. 

8. Finally we `|>` the output of the `Enum.map` stage of our pipeline to `Enum.into/2`. Again, the output is passed in as the *first* argument for `Enum.into` function, the second argument being an empty string `""`. `Enum.into` is useful for massaging data into a form that we desire. In this instance we want the list output from `map` transformed back into a string.

See how we are using solution to simpler sub-problem in solving a bigger one? Also notice how we create a pipeline of functions (some that came included with Elixir and some that we wrote on our own) that transforms the input into the final result, in a series of smaller steps. We will continue to use this approach on our quest for an overall solution to decoding our Caesar cyper encypted sentence.

### Version 0.3

In this version, we will build up on the previous code by adding the top level function that will decode an entire sentence for us.

```elixir
defmodule TextAlchemy do
  @moduledoc """
  Provides text manipulation related functions.
  """

  @doc """
  Rotates a single character by 13 positions.
  """
  def rot13c(character) do

    case character do
       "a" -> "n"
       "b" -> "o"
       "c" -> "p"
       "d" -> "q"
       "e" -> "r"
       "f" -> "s"
       "g" -> "t"
       "h" -> "u"
       "i" -> "v"
       "j" -> "w"
       "k" -> "x"
       "l" -> "y"
       "m" -> "z"
       "n" -> "a"
       "o" -> "b"
       "p" -> "c"
       "q" -> "d"
       "r" -> "e"
       "s" -> "f"
       "t" -> "g"
       "u" -> "h"
       "v" -> "i"
       "w" -> "j"
       "x" -> "k"
       "y" -> "l"
       "z" -> "m"
       _   -> :error
    end
  end

  @doc """
  Applies the rot13c function to all characters in a word
  """
  def rot13w(word) do

   word
   |> String.downcase
   |> String.graphemes
   |> Enum.map(&rot13c/1)
   |> Enum.into("")

  end

  @doc """
  Encodes or decodes a string (of words) using rot13
  """
  def rot13(sentence) do

    sentence
    |> String.split
    |> Enum.map(&rot13w/1)
    |> Enum.join(" ")
  end
end

# Decoding the second line from the Alchemist's message
IO.puts TextAlchemy.rot13("Gur havirefr pbafcverf gb uryc gur Qernzre")

```

#### Notes on Version 0.3

1. We will reach for our favorite `|>` based pipeline approach for the `rot13` function that will take in a sentence and return the decoded sentence for us.
2. `String.split/1` splits a sentence into a list of words. 
3. We pass in `rot13w/1` function that we built in the previous section to `Enum.map` so that we transform (decrypt) each word in the list.
4. We use `Enum.join/2` which will join the words in the list into a string with the specified character plugged in between (which in this case is a space character `" "`). 

Run this code in a terminal and read the decoded sentence for yourselves! I won't spoil the fun by printing the result here.

### Version 0.4

In general, we don't want to be copy pasting the text from the Alchemist's scroll into our code. Ideally we would love our program to read the text from a file containing the message and decrypt the contents for us. We will explore how to use the file reading capabilities of Elixir in our final version.

```elixir
defmodule TextAlchemy do
  @moduledoc """
  Provides text manipulation related functions.
  """

  @doc """
  Rotates a single character by 13 positions.
  """
  def rot13c(character) do

    case character do
       "a" -> "n"
       "b" -> "o"
       "c" -> "p"
       "d" -> "q"
       "e" -> "r"
       "f" -> "s"
       "g" -> "t"
       "h" -> "u"
       "i" -> "v"
       "j" -> "w"
       "k" -> "x"
       "l" -> "y"
       "m" -> "z"
       "n" -> "a"
       "o" -> "b"
       "p" -> "c"
       "q" -> "d"
       "r" -> "e"
       "s" -> "f"
       "t" -> "g"
       "u" -> "h"
       "v" -> "i"
       "w" -> "j"
       "x" -> "k"
       "y" -> "l"
       "z" -> "m"
       _   -> :error
    end
  end

  @doc """
  Applies the rot13c function to all characters in a word
  """
  def rot13w(word) do

   word
   |> String.downcase
   |> String.graphemes
   |> Enum.map(&rot13c/1)
   |> Enum.into("")

  end

  @doc """
  Encodes or decodes a string (of words) using rot13
  """
  def rot13(sentence) do

    sentence
    |> String.split
    |> Enum.map(&rot13w/1)
    |> Enum.join(" ")
  end
end

# Decoding the Alchemist's message
# --------------------------------

# Reading the filename containing the message from commandline
[filename | _tail] = System.argv

# Open the file for reading
{:ok, file} = File.open(filename, [:read])

# Read the first line of the message from the file
line_1 = IO.read(file, :line) |> String.trim

# Print the first line from the Alchemist's message, as it is
IO.puts line_1

# Print the second line from the Alchemist's message, after decoding
line_2 = IO.read(file, :line) |> String.trim
IO.puts TextAlchemy.rot13(line_2)

```

#### Notes on Version 0.4

Lines 1 to 65 are unchanged. Let's focus on the rest. 

1. The filename of the file (or scroll) containing the message needs to be passed into our program as an input. We have two options to do this. 1) Pass in the filename as an *argument* to the program, before the program runs. 2) Read in the filename as a *user input* while the program is running, using `IO.gets`, the counterpart of `IO.puts`. For now let's explore the command line argument way of doing this.
2. `System.argv` is an included function in Elixir which will return the command line arguments passed in as a list. What I mean by commmand line argument is, the set of strings passed in while running the Elixir program at the command line. For example, so far we ran our elixir programs by entering the following into the terminal

  ```terminal
  elixir 7.ex
  ```
  
  We can optionally pass in stuff to this program that we are about to start running like so:
  
  ```terminal
  elixir 7.ex scroll_1 option_a option_b
  ```
  
  `System.argv` will return the list `["scroll_1", "option_a", "option_b"]` in this case.

3. Line 71 is another creative example of pattern matching that once we understand clearly, will make life simpler for us. On the LHS (left hand side) of the match operator `=`, we have `[filename | _tail]`. We know the RHS (right hand side is a list `["filename", "option1", "option2"]`. Elixir uses the `|` within a list to separate what it calls the `head` and `tail` of a list. `head` of a list is simply the very first element in the list. `tail` is a *list* contiaing the rest of the items in the list (aka "the tail"). So while pattern matching Elixir will bind the value `"scroll_1"` to `filename` and the *list* `["option_a", "option_b"]` to `_tail`. We have the underscore `_` in `_tail` since we don't plan to use the `_tail`. We are only interested in the first element of the argument list which will contain the filename of the scroll contiaing the message from the Alchemist.

4. On Line 74, we encounter **tuples** for the first time. **Tuple** is another basic datatype in Elixir. Tuples are used to group together simpler data types into one unit. Tuples are like lists in that they can contain any value, be it an atom, string etc. While lists are mostly used when the number of elements it contains may grow or shrink during the program's life, tuples are best used when the size (the number of elements in a tuple) is known before the program runs. A very common use case for tuples is to return more than one value from a functionc all, as is the case here.
5. On Line 74, we encounter a case of pattern matching involving a tuple. The function `File.open(filename, [:read])` will open a file specified by the filename for reading and return the result as either `{:ok, file}` or another tuple containing the error message. `file` holds what is more generally known as a *file reference*, a data type used by Elixir that points to a file on our computer. Elixir will pattern match and check if the function call succeeded (the first element in case of success should be the atom `:ok`) and if so, bind the variable `file` to the *file reference* returned by the `File.open` function.
6. We then proceed to read from the file using `IO.read` function. It takes two aguments, the filename and a list of options. We pass in the atom :read to specify that we are opening this file for reading. 
7. `String.trim` removes newline character at the end of the line being read. Else we would have two newlines when we print it out. We read the first line and print it out as is. The second line, which was encoded, is printed out after decoding it.
8. For all the functions we have seen so far, from the modules `String`, `Enum`, `File`, `IO` etc, it is very advisable to read through the documentation to learn more about how to use the function effectively. Documentation will describe the input and output parameters, default behaviour of the function, optional parameters that can be passed into alter the default behavior of the functions etc. Documentation is readily available for all Elixir modules at [hexdocs](https://hexdocs.pm/). For example you can view documentation about functions in the [Enum module here](https://hexdocs.pm/elixir/Enum.html). Elixir comes with a lot of "batteries" included. For many thing you may want to do, there might already be a function in one of the modules bundled with Elixir. The investment you put in to get familiar with the documentation will pay high returns in your journey of elixir & programming mastery.

## Running and Output

Create a plain text file named `message.txt` and copy  the message from the alchemist into the text file. Remember to pass in this filename to your program while running the final version of the code.


```terminal
Charlie@Aruns-MacBook-Pro-2 ~/studios/bookofemerald/lib/ex $ elixir 7.ex message.txt
                                       
Thirteen times of what you have Left, you owe Caesar
the universe always conspires to help the dreamer
```
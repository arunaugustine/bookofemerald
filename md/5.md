# Ex 5: Logic of Logistics

The boy took out the black and white stones the old king had given him.

> "They are called Urim and Thummim. The black signifies 'yes,' and the white 'no.' When you are unable to read the omens, they will help you to do so. Always ask an objective question.”
> 
>  -- Paulo Coelho, The Alchemist

The boy, Santiago, now had to choose the road he will take to Tarifa, the town where the gypsy woman lived. But the boy had decided not to use the stones. He had promising himself that he will make his own decisions and be the master of his own destiny. Instead, he decided to call upon the ancient art of **logic** to arrive at his decision.

## Coding Exercise
Maybe you have already noticed that the number of lines of code that you are painstakingly typing in line by line has been increasing slowly. This is to help build your coding chops slowly over time. This is just like how you would build your muscles in a gym by increasing the weights over time or working to improve your stamina by running a little bit further every day. 

But typing in all 56 lines at once and trying to run the whole program might throw up a lot of errors at once. This might be a frustrating experience, even while attempting to **work the problem**. The key strategy to employ here is to *break the whole into smaller parts* and getting each part working before proceeding to add the next. 

For instance type in lines 1-19 first. Run and observe the output. Once you convince yourself that you really understand the code and the output, add in lines 20-32. Run and Observe. Add in 33-44. Do the same. And finally Lines 45-56. Run and observe the whole output.


Have faith in the process and remember to *enjoy* the journey up the mountain.


> “To live only for some future goal is shallow. It’s the sides of the mountain which sustain life, not the top. Here’s where things grow.” 

> ― Robert M. Pirsig, Zen and the Art of Motorcycle Maintenance

```elixir
# The Road to Tarifa


# Estimating the distance (in Km.) between the
# Sycamore (Sycamore tree where the treasure is),
# Seville (a city, where the boy can buy a horse) and
# Tarfia (the town where the gypsy woman lives)

distance_sycamore_to_seville = 300
distance_seville_to_tarifa = 400
distance_sycamore_to_tarifa = 500


is_horse_faster_than_boy? = true
walking_with_treasure_easier? = false

IO.puts "So it is #{is_horse_faster_than_boy?} that riding a horse is faster than the boy walking."
IO.puts "and it is #{ walking_with_treasure_easier?} to say, carrying a heavy treasure is easier while walking than riding."

# Is the route from Sycamore to Tarifa via Seville
# shorter than the direct path from Sycamore to Tarifa?

# Let us work out the math and find out.
route_via_seville_shorter? = distance_sycamore_to_seville + distance_seville_to_tarifa < distance_sycamore_to_tarifa
IO.puts "Is route via Seville shorter than the direct path between Sycamore and Tarifa? : #{route_via_seville_shorter?}"

# The boy has two options.
plan_a = "From Sycamore, walk to Seville. Buy a horse there and ride to Tarifa"
plan_b = "Walk along the direct route from Sycamore to Tarifa"
IO.puts "Plan A is: #{plan_a}"
IO.puts "Plan B is: #{plan_b}"

horse_speed_factor = 4 # Horse is 4 times faster than walking.
IO.puts "The horse is #{horse_speed_factor} times faster than walking"

days_to_walk_100km = 8 # It takes 8 days to walk 100 km
days_to_ride_100km = days_to_walk_100km / horse_speed_factor # How man days to ride 100km?

days_plan_a =  ( (distance_sycamore_to_seville / 100) * days_to_walk_100km) + ( (distance_seville_to_tarifa / 100) * days_to_ride_100km)
days_plan_b = (distance_sycamore_to_tarifa / 100) * days_to_walk_100km

IO.puts "Travel time for Plan A: #{round(days_plan_a)} days"
IO.puts "Travel time for Plan B: #{round(days_plan_b)} days"

IO.puts "Does Plan A take less time than Plan B? : #{days_plan_a < days_plan_b}"
IO.puts "Does Plan A take more time than Plan B? : #{days_plan_a > days_plan_b}"

IO.puts "The boy decided to pick the plan that has the shorter travel time."

decision_taken = if(days_plan_a < days_plan_b) do
  "The boy chose Plan A: \n  \"#{plan_a}\""
else # if it was the other way around
  "The boy chose Plan B: \n  \"#{plan_b}\""
end

IO.puts decision_taken

```

## Things to Note

1. `true` and `false` are known as **booleans**. Boolean is a basic *type* of data just like **strings**, **integers** and **floats**. Other basic types in Elixir are **atoms**, **lists** and **tuples**. We will learn about these as we go along.
2. Variables can store any of these basic types. For e.g, `distance_sycamore_to_seville` stores **integer** data, `is_horse_faster_than_boy?` stores a **boolean** (which can either be `true` or `false`) and `plan_a` stores **string** data (so does `plan_b`).
3. To find the lesser or greater of two numbers we can use the `<` *lesser-than* operator and `>` *greather-than* operator, respectively. They compare two numbers and return either `true` or `false`. For e.g
  
  ```iex
  iex(1)> 3 < 4
  true
  iex(2)> 4 < 3
  false
  iex(3)> 3 > 4
  false
  iex(4)> 4 > 3
  true
  ``` 

4. Other available comparison operators are:
 -  `<=` **lesser-than-or-equal-to**
 -  `>=` **greater-than-or-equal-to** 

5. If we want to find out of two numbers are equal, we have two options:
 -  `==` **equal-to** also known as "double equals"
 -  `===` **strictly-equal-to** also known as "triple equals"
    
    The **strictly-equal-to** operator is used, for e.g, in scenarious where we compare an integer with a float and we have a choice as to either do a *simple* comparison or a *strict* comparison:
   
    ```iex
    iex(1)> 1 == 1.0
    true
    iex(2)> 1 === 1.0
    false
    ```
 

6. You can "invert" boolean logic by using the `!` operator.
  
  ```iex
  iex(1)> !true
  false
  iex(2)> !false
  true
  iex(3)> 3 != 3 # not-equal-to
  false
  iex(4)> 3 != 4
  true
  iex(5)> 3 !== 3.0 # strictly-not-equal-to
  true
  iex(6)> 3 !== 3
  false
  ```   
7. In Elixir, we use a `if-else` construct (lines 50-54) to represent a *logical* choice. Let's look at a bare-bones example:
    
    ```elixir
    result  =  if ( is_this_true? ) do
                  "Yes it is true!"
               else
                  "No, it's not!"
               end
    ```
   Elixir first evaluates `is_this_true?`, the expression or variable between the brackets `( )` just after "**if**". If `is_this_true?` evaluates to `true`, then **if-else** expression's *value* (which is another name for *result after evaluation*) is the string `"Yes it is true!"`. If `is_this_true?` evaluates to `false` then **if-else** expression's value is the string `"No, it's not!"`. In both the cases, the value of the **if-else** expression, the resulting string,  is matched to the variable `result`.
   
8. Notice the weird looking `\n` and `\"` within the string on lines 51 and 53. `\n` asks Elixir to insert a "new line" into the string. That is after the `\n` character, Elixir prints the rest of the string on the next line. `\"` prints a literal `"` character. We have to use the `\` to *escape* so that Elixir doesnot confuse it with the closing `"` that matches one at the beginning of the string. Similarly if you want to literally print a `\` character your have to *escape* it using `\\` within the string. There are other escape sequnces to denote other things like space `\s`, tab `\t` (longer white-space) and `\uDDDD` for printing *Unicode codepoints* (Unicode is a universally agreed look-up table to print any printable characters using its curresponding codepoint)
    
    ```iex
    iex(1)> IO.puts "\n\t\u2661\n"

        ♡

    :ok
    ```

## Running and Output
Save the coding exercise in a file `5.ex` and run it from the command line using `elixir 5.ex`.

```output
Charlie@Aruns-MacBook-Pro-2 ~/studios/bookofemerald/lib/ex $ elixir 5.ex                     
So it is true that riding a horse is faster than the boy walking.
and it is false to say, carrying a heavy treasure is easier while walking than riding.
Is route via Seville shorter than the direct path between Sycamore and Tarifa? : false
Plan A is: From Sycamore, walk to Seville. Buy a horse there and ride to Tarifa
Plan B is: Walk along the direct route from Sycamore to Tarifa
The horse is 4 times faster than walking
Travel time for Plan A: 32 days
Travel time for Plan B: 40 days
Does Plan A take less time than Plan B? : true
Does Plan A take more time than Plan B? : false
The boy decided to pick the plan that has the shorter travel time.
The boy chose Plan A:
  "From Sycamore, walk to Seville. Buy a horse there and ride to Tarifa"
```

## Output Observations

1. Try changing the `horse_speed_factor` to `2`. Does the boy stick with plan A in the case?
2. Suppose you want to write the **if** condition as `if( days_plan_b < days_plan_a )` how would you re-write the **if-else** expression for the logic to return the correct result?
3. Notice that string interpolation using # { } works for other basic data types like boolean, integer and floats also, apart from string variables. This works because, behind the scenes, Elixir is being helpful by converting boolean, integer and float data into equivalent string representations.
4. The resulting value of the **if-else** expression (which is a string) is matched to `decision_taken`. The last line of code prints the value of `decision_taken` on the screen. Suppose you change the code (lines 50-56) to be like the following:
  
  ```elixir
  decision_taken = if(days_plan_a < days_plan_b) do
      IO.puts "The boy chose Plan A: \n  \"#{plan_a}\""
  else # if it was the other way around
      IO.puts "The boy chose Plan B: \n  \"#{plan_b}\""
  end

  IO.puts decision_taken

  ```
What do you think will be printed onto the screen due the last line? Hint: Did you notice a new `ok` getting printed on the last line? Why do you think that is? Its ok (☺) if you can't figure this out just yet. You will learn more about **atoms**, another basic data type, in the next exercise which will help you solve this mystery.

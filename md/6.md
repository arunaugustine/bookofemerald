# Ex 6: Atoms and Modules

"Alchemy is both art and science", the Alchemist once told the boy. 

"Its the science of breaking down a substance into simpler constituents, refining it until only the most basic elements remain, identifying or naming these elements and finally re-composing these elements to form a higher quality substance". 

"So where is art in this?", the boy asked. 

"Art is knowing which substance to start with. Art is knowing when to stop refining. Art is naming the elements right. And, art is choosing how best to combine the elements again."


## Seeking to Understand

> Begin with the end in mind. But if you don't know how to begin, start with the middle. Maybe its a circle and has no beginning or end. All it may have is the *nearest* middle.

> -- Charlie


1. In the English language we have a progression from *alphabet* to *words* to *sentences* to *paragraphs* to *chapters* to *books*. To use a loose analogy, in programming we correspondingly have **symbols**, **basic data types**, **expressions**, **functions**, **modules** and **applications**.

1. Numbers like `1`, `2.5` etc can be thought of as symbols having the integer value of 1, floating point value of 2.5 etc. They are also called *constants*. This is because their *value* does not change over time. *Value* is what Elixir returns (e.g line 2 and 4 below) as a result of evaluating an expression or symbol (lines 1 and 3 below). In the case of constants, each time you type in the following into IEx, you get the same results:

  ```iex
  iex(1)> 1
  1
  iex(2)> 2.5
  2.5
  ```

1. **Atoms** are constants where their name is their own value.
   
   ```iex
  iex(1)> :hello
  :hello
  iex(2)> :ok
  :ok
  iex(3)> IO.puts :ok
  ok
  :ok
  iex(4)> IO.puts "ok"
  ok
  :ok
  ```
1. One use case of **atoms** is to denote success or failure of a function call. For instance, as shown above, `IO.puts` will return `:ok` if it was successful in printing the given string to the screen. Remember the mystery "ok" from our last exercise? (Ex 5, Output Observation 5).


## Coding Exercise

In the following coding exercise, we will learn about creating our own **functions**. **Function** is another name for *procedure that manipulates data*. Functions always come packaged in **modules**, much like how paragraphs in an english text are grouped into chapters. You will come across some keywords like `defmodule`, `def`, `do`, `end` etc in the code. Pay close attention to how Elixir uses these keywords to *specify* functions and modules. In general, the rules around how to use these keywords comprises of what is known as *syntax* of a programming language. We won't try to study these rules per-say. Instead, as usual, we will learn through practice.

### Version 0.1

Type in and save the following code in a file named `6.ex` and run it using `elixir 6.ex`. Observe the output (not listed here).

```elixir
defmodule MathAlchemy do

  def mod(a, b) do
    d = div(a, b)
    a - (b * d)
  end

end # End module

IO.puts MathAlchemy.mod(5,3)

```

#### Notes on Version 0.1 

1. `defmodule` specifies a module. Module names are to be *CamelCase* (First letter of everyword is capitalized, with punctuations and spaces removed). The *body* or contents of the module is surrounded with `do` and `end`.
2. `def` specifies a function. `do` and `end` surrounds the function *body*. 
3. The variables inside the brackets `( )` are called function *parameters*. They are placeholders for *input* data that the expressions within the function *body* transforms to an *output* which is *returned* by the function.
4. Function names are to be *snake_case* (All small letters with `_` between words).
5. The value of the very last expression within the function *body* is what is considered as the output value of the function which is *returned*. For instance, here the resultant value of `a - (b * d)` which is an integer value is returned by the `mod` function.
6. As seen on line 10, a function is referenced outside the module by referencing the module name (`MathAlchemy`), followed by a dot `.` and then the function name along with the input parameters, `mod(5,3)`.
7. `div` is a built-in Elixir function that performs integer division (fractional part after decimal point of the quotient is not considered). Try `div( 5, 2)` and perhaps a few other examples in an IEx shell by yourself to really understand how `div` works. Also try `h div`. IEx will printout helpful documentation (`h` for help) along with examples for the function `div`.
8. "Built-in" functions in Elixir are bundled in the `Kernel` module. Functions in the `Kernel` module are special in that you can use them directly anywhere in your Elixir code without using `Kernel.` (dot syntax) before the function name. 

### Version 0.2
Let us improve on version 0.1 by adding in some code documentation. In the same `6.ex` source code file, add or modify lines of code as follows. If you would rather have some practice typing in code, you may delete the contents that you have so far for version 0.1 of `6.ex` and start fresh with version 0.2. The decision is upto you.

```elixir
defmodule MathAlchemy do
  @moduledoc """
  Provides alchemy related math procedures.

  Important functions include mod (modulo or remainder after integer division),
  gcd (greatest common divisor), prime? (is it a prime number?) etc.
  """
  # --------------------------------------------------------------

  @doc """
  Calculates the mod (modulo or remainder after integer division).

  For instance integer division of 5 by 2 is equal to 2 with remainder 1.
  So for example:

    iex> MathAlchemy.mod(5,2)
    1
  """
  def mod(a, b) do
    q = div(a, b) # q for quotient
    a - (b * q)
  end

end # End module

IO.puts MathAlchemy.mod(5, 2)
IO.puts MathAlchemy.mod(5, 3)
IO.puts MathAlchemy.mod(5, 5)

```

#### Notes on Version 0.2

1. Comments (lines starting with `#`) offer one way to document code, as we have already seen. For a more specific use case of documenting modules `@moduledoc` is used. Likewise, `@doc` is used to document functions.
2. Triple quotes `"""` are used to create *multi-line* text that preserves linebreaks, any whitespace (space or tab) including any indentation added for readability. The text you create will print just as you typed in, space, tab, newline character and all.

  ```iex
  iex(1)> IO.puts """
  ...(1)> A multi-line text
  ...(1)> is also called
  ...(1)> a heredoc.
  ...(1)> """
  A multi-line text
  is also called
  a heredoc.
  
  :ok
  ```

3. Notice how we added example usage of the function `mod` on line 16. Particularly, there are two spaces added before `iex` and the evaluated value just below the expression. We will see *why*, soon.
4. We have added more `IO.puts` to test a few other sample inputs to convince ourselves that our `mod` function is working as expected. It's important to test for a variety of inputs including edge cases like passing in `0` or negative numbers as values of `a` or `b`. We will improve our `mod` function implementation to handle negative numbers in version 0.3

### Version 0.3
Carefully read the code listed below line by line. Some existing lines of code has been modified. New lines of code and comments have been added. Make sure when you update your `6.ex` file from version 0.2 to 0.3, the final code looks like the version 0.3 code listed below.

```elixir
defmodule MathAlchemy do
  @moduledoc """
  Provides alchemy related math procedures.

  Important functions include mod (modulo or remainder after integer division),
  gcd (greatest common divisor), prime? (is it a prime number?) etc.
  """

  # --------------------------------------------------------------

  @doc """
  Calculates the mod (modulo or remainder after integer division).

  For instance integer division of 5 by 2 is equal to 2 with remainder 1.
  So for example:

    iex> MathAlchemy.mod(5,2)
    1
  """
  def mod(a, b) do
    q = div( abs(a), b) # q for quotient
    abs(a) - (b * q)
  end

  @doc """
  Run tests for mod function. We could potentially
  add more tests here in the future.
  """

  def test_mod() do
    IO.puts "mod(5,2) expected: 1  actual: #{MathAlchemy.mod(5, 2)}"
    IO.puts "mod(-5,2) expected: 1 actual: #{MathAlchemy.mod(-5, 2)}"
    IO.puts "mod(5,-3) expected: 2  actual: #{MathAlchemy.mod(5, -3)}"
    IO.puts "mod(5,5) expected: 0  actual: #{MathAlchemy.mod(5, 5)}"
    :tests_completed
  end

end # End module

MathAlchemy.test_mod()

```

#### Notes on Version 0.3

1. On line 21, instead of passing in plain `a` as input, we pass in the result of calling the `abs` function on `a` as input parameter to `mod`. `abs` returns the absolute value of a number (negative signs are ignored). Explore how `abs` works on an IEx shell. Perhaps try using `h abs` to read the documentation and example usages of `abs` function.
2. We have also added a new function named `test_mod` which takes in zero input parameters. We have moved all our test `IO.puts` expressions into this one function. Combining expressions into functions is the analogue of combinging sentences into paragraphs in English. 
3. Our `test_mod` function returns an **atom** called `:tests_completed`. This is a way to signal the expression that "called" this function (in our case line 40) that it has successfully completed its "mission".
4. We have added a few more test scenarios, now including negative numbers as inputs to `mod` function. Verify that the results are correct from the output by running `elixir 6.ex` after saving your work. Output from this version of code is not listed here, as with version 0.1 and 0.2. An important skill to aquire is of convicing ourselves that our work is right by checking the steps and playing with the output we get rather than comparing the result with an "official" output. This way you will learn to trust yourselves and build confidence in your own coding and debugging skills.  

### Version 0.4

What happens if `0` is passed in as input for `b`? Try it out with version 0.3 and notice what happens. In version 0.4 we will improve our code further to handle this case correctly. Also we will figure out a way to make our test case writing a little easier. As usual, make changes to the same file `6.ex`, by carefully adding or modifying lines of code to match the version 0.4 code listed below. 

```elixir
defmodule MathAlchemy do
  @moduledoc """
  Provides alchemy related math procedures.

  Important functions include mod (modulo or remainder after integer division),
  gcd (greatest common divisor), prime? (is it a prime number?) etc.
  """
  # --------------------------------------------------------------

  @doc """
  Calculates the mod (modulo or remainder after integer division).

  For instance integer division of 5 by 2 is equal to 2 with remainder 1.
  So for example:

    iex> MathAlchemy.mod(5,2)
    1
  """
  def mod(a, b) when b > 0 do
    q = div( abs(a), b) # q for quotient
    abs(a) - (b * q)
  end

  def mod(_a, b) when b <= 0 do
    :error
  end

  @doc """
  Run tests for mod function. We could potentially
  add more test here in the future.
  """
  def test_mod() do
    IO.puts mod_test_case(5, 2, 1)
    IO.puts mod_test_case(-5, 2, 1)
    IO.puts mod_test_case(5, 3, 2)
    IO.puts mod_test_case(5, 5, 0)
    IO.puts mod_test_case(-5, 4, 1)
    IO.puts mod_test_case(-5, -4, :error)
    IO.puts mod_test_case(5, 0, :error)
    :tests_completed
  end

  # This is a private function which can be used only within MathAlchemy module.
  # `mod_test_case` takes as input parameters, the numbers a, b and the
  # expected output from mod and returns a test string to be printed out
  defp mod_test_case(a,b, expected) do
    "mod(#{a},#{b}) expected: #{expected}  actual: #{mod(a, b)}"
  end

end # End module

MathAlchemy.test_mod()

```

#### Notes on Version 0.4

1. On line 19, notice how we have added `when b > 0` to the `mod` function definition. `when` is how you specify **guard clauses**. Guard clauses allow the function to be executed only when the condition specified evaluates to `true`. That is this function definition for `mod` is used only if `b > 0`. 
2. On line 24, we have *another* `mod` definition. (Function definition is another name for *specifying* a function). Notice how the guard clause for this second `mod` function declaration handles the *other* case of `b <= 0`. 
3. The second clause of `mod` definition on line 24 to 26 ensures that if we pass in `0` or a negative number of `b`, the function returns `:error` (an **atom**).
4. Did you notice the `_` underscore before `a` on line 24? This is because we want to let Elixir know that we can safely ignore the value of `a`. Irrespective of what the value of `a` is an `:error` is returned if `b` is `0` or negative. See how `a` appears no where in the function body. You could have also just written `(_ , b)` but then that would have been confusing to your future readers. Hence we include the variable name just after the `_` even though Elixir does not require us to.
5. Instead of repeating the same test string over and over, we have delegated generating the test string to a function named `mod_test_case`.
6. Notice how `mod_test_case` is defined using `defp`. `defp` is how you create **private** functions. Private functions can only be used by functions defined in the same module. They are not "visible" outside the module.
7. `mod_test_case` takes 3 inputs: `a` and `b` which are the same as inputs to `mod` and `expected` which is the result that we expect `mod` to return when we pass in `a` and `b`. Notice how we construct a test string using `#{}` interpolation. Also notice how we call `mod_test_case` function on lines 33 to 39 to cover a wide variety of test inputs. When the program runs, we should be able to visually inspect the output and decide whether `mod` is working correctly or not.
8. Notice how the last expression within the function definition is the **return** value of the function. Hence line 21 of the first clause of `mod` function definition returns a number and line 25 of the second clause of `mod` function definition returns an atom. The function `test_mod` returns an atom on line 40 and the private function `mod_test_case` returns a **string**.

## Running and Output

Run the final `6.ex` file using `elixir 6.ex` and compare your output with the one listed below.

```output
Charlie@Aruns-MacBook-Pro-2 ~/studios/bookofemerald/lib/ex $ elixir 6.ex                             
mod(5,2) expected: 1  actual: 1
mod(-5,2) expected: 1  actual: 1
mod(5,3) expected: 2  actual: 2
mod(5,5) expected: 0  actual: 0
mod(-5,4) expected: 1  actual: 1
mod(-5,-4) expected: error  actual: error
mod(5,0) expected: error  actual: error
```


## Version by Version

> “Thirty years ago my older brother, who was ten years old at the time, was trying to get a report written on birds that he'd had three months to write, which was due the next day. We were out at our family cabin in Bolinas, and he was at the kitchen table close to tears, surrounded by binder paper and pencils and unopened books about birds, immobilized by the hugeness of the task ahead. Then my father sat down beside him put his arm around my brother's shoulder, and said, **"Bird by bird, buddy. Just take it bird by bird.”** 

> ― Anne Lamott, Bird by Bird: Some Instructions on Writing and Life


Writing code is almost always an exercise of progressively improving on our work, version by version. Programming is nothing but translating our thoughts into a language for the computer to execute on our behalf. And just like in writing, the thoughts coalese and organize themselves in the process of writing it out. So don't worry or let your brain freeze over how big a task you are starting out on. Pick one starting point and begin. Code a little. Test a little. Build your work of art, *version by version*.